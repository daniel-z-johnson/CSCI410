// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
			Not16(in=x, out=notPrex);
			And16(a=x, b=notPrex, out=zero16);
			Mux16(a=x, b=zero16, sel=zx, out=xstageOne);
			Not16(in=xstageOne, out=notxstageOne);
			Mux16(a=xstageOne, b=notxstageOne, sel=nx, out=finX);
			
			Mux16(a=y, b=zero16, sel=zy, out=ystageOne);
			Not16(in=ystageOne, out=notystageOne);
			Mux16(a=ystageOne, b=notystageOne, sel=ny, out=finY);

			And16(a=finX, b=finY, out=andXY);
			Add16(a=finX, b=finY, out=addXY);
			Mux16(a=andXY, b=addXY, sel=f, out=preLast);

			Not16(in=preLast, out=notpreLast);
			Mux16(a=preLast, b=notpreLast, sel=no, out=out, out[0..7]=low, out[8..15]=high, out[15]=ng);

		Or8Way(in=low, out=p1);	
		Or8Way(in=high, out=p2);
		Or(a=p1, b=p2, out=prezr);
		Not(in=prezr, out=zr);
}
